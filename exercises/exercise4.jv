transform CelsiusTofahrenheit {
  from tempCelsius oftype decimal;
  to tempFahrenheit oftype decimal;

  tempFahrenheit: ((tempCelsius * 9/5) + 32);
}

constraint monatConstraint oftype RangeConstraint {
    lowerBound: 1;
    upperBound: 12;
}

valuetype monat oftype integer {
    constraints: [monatConstraint];
}

pipeline Exercise4Pipeline {
    block DataExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }
    block TemperatureZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }
    block TemperatureFilePicker oftype FilePicker {
        path: "/data.csv";
    }
    block TemperatureTextFileInterpreter oftype TextFileInterpreter {
	      encoding: "utf8";
	}
    block TemperatureCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }
    block TemperatureColumnDeleter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I,  column L, column M, column N, column O];
    }
	
	block TemperatureRowDeleter oftype RowDeleter {
        delete: [row 1];
    }
    block TemperatureTableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "Geraet" oftype integer,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype monat,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype text,
        ];
    }

    block CelsiusToFahrenheit oftype TableTransformer {
        inputColumns: ['Temperatur'];
        outputColumn: 'Temperatur';
        use: CelsiusTofahrenheit;
    }

    block BattCelsiusToFahrenheit oftype TableTransformer {
        inputColumns: ['Batterietemperatur'];
        outputColumn: 'Batterietemperatur';
        use: CelsiusTofahrenheit;
    }

    block DBLoader oftype SQLiteLoader { table: "temperatures"; file: "./temperatures.sqlite"; }

    DataExtractor
        ->TemperatureZipArchiveInterpreter
        ->TemperatureFilePicker
        ->TemperatureTextFileInterpreter
        ->TemperatureCSVInterpreter
        ->TemperatureColumnDeleter
        ->TemperatureRowDeleter
        ->TemperatureTableInterpreter
        ->CelsiusToFahrenheit
        ->BattCelsiusToFahrenheit
        ->DBLoader;
}
